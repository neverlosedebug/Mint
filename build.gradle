plugins {
    id 'java'
    id 'fabric-loom' version '1.10.1'
    id 'com.gradleup.shadow' version '9.0.0-beta4'
    id 'io.freefair.lombok' version '8.11'
    id 'com.github.gmazzo.buildconfig' version '5.5.1'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven {
        name = "meteor-maven"
        url = "https://maven.meteordev.org/releases"
    }
    maven {
        name = "meteor-maven-snapshots"
        url = "https://maven.meteordev.org/snapshots"
    }
    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    implementation(shadow("org.luaj:luaj-jse:3.0.1"))
    implementation(shadow("org.reflections:reflections:0.10.2"))
    implementation(shadow("meteordevelopment:discord-ipc:1.1"))

    implementation(shadow("org.bouncycastle:bcprov-jdk18on:1.78.1"))
    implementation(shadow("org.bouncycastle:bcpkix-jdk18on:1.78.1"))
}

def getCommitHash = { ->
    def gitProcess = 'git rev-parse HEAD'.execute()
    gitProcess.waitFor()
    return gitProcess.text.trim().take(10)
}

def getRevisionCount = { ->
    def gitProcess = 'git rev-list --all --count'.execute()
    gitProcess.waitFor()
    return gitProcess.text.trim()
}

buildConfig {
    className("BuildConstants")
    packageName(group)
    buildConfigField(String, 'MOD_VERSION', project.mod_version)
    buildConfigField(String, 'MINECRAFT_VERSION', project.minecraft_version)
    buildConfigField(String, 'GIT_HASH', getCommitHash())
    buildConfigField(String, 'GIT_REVISION', getRevisionCount())
}

processResources {
    inputs.properties([
            "version": version,
            "minecraft_version": project.minecraft_version,
            "loader_version": project.loader_version
    ])
    filteringCharset = "UTF-8"

    filesMatching("fabric.mod.json") {
        expand([
                "version": version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

loom {
    accessWidenerPath = file("src/main/resources/mint.accesswidener")
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    enabled = false
}

shadowJar {
    configurations = [project.configurations.shadow]
    archiveClassifier = ''
}

remapJar {
    dependsOn(shadowJar)
    inputFile = shadowJar.archiveFile
    archiveClassifier = ''
    archiveFileName = "${archives_base_name}-${version}.jar"
}

tasks.build {
    dependsOn(remapJar)
}